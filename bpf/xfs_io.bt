#!/usr/bin/env bpftrace

BEGIN
{
  printf("COMM=xfskv_test\n");
}

kprobe:iomap_dio_rw
/ comm == "xfskv_test" /
{
    @dio_seq[pid] = @dio_seq[pid] + 1;
    @dio_cur[pid] = @dio_seq[pid];
    @dio_fs_t0[pid, @dio_cur[pid]] = nsecs;
}

tracepoint:nvme:nvme_setup_cmd
/ comm == "xfskv_test" /
{
    $d = @dio_cur[pid];
    if ($d > 0) {
        $ctrl_id = args->ctrl_id;
        $qid = args->qid;
        $cid = args->cid;
        @start[pid, $d, args->ctrl_id, args->qid, args->cid] = nsecs;
        @cmd_owner_pid[$ctrl_id, $qid, $cid] = pid;
        @cmd_owner_dio[$ctrl_id, $qid, $cid] = $d;
    }
}

tracepoint:nvme:nvme_complete_rq
{
    $ctrl_id = args->ctrl_id;
    $qid = args->qid;
    $cid = args->cid;
    $p = @cmd_owner_pid[$ctrl_id, $qid, $cid];
    $d = @cmd_owner_dio[$ctrl_id, $qid, $cid];
    $t1 = @start[$p, $d, $ctrl_id, $qid, $cid];
    if ($p && $d && $t1) {
        $lat_ns = nsecs - $t1;
        @dio_cnt[$p, $d]  = @dio_cnt[$p, $d]  + 1;
        @tot_lat_ns[$p, $d] = @tot_lat_ns[$p, $d] + $lat_ns;
        delete(@start[$p, $d, $ctrl_id, $qid, $cid]);
        delete(@cmd_owner_pid[$ctrl_id, $qid, $cid]);
        delete(@cmd_owner_dio[$ctrl_id, $qid, $cid]);
    }
}

kretprobe:iomap_dio_rw
/ comm == "xfskv_test" && @dio_cur[tid] > 0 /
{
    $d = @dio_cur[pid];
    $ret = retval;                        // >=0 同步完成
    $fs_e2e = nsecs - @dio_fs_t0[pid, $d];
    printf("[DIO] tid=%d dio=%d bytes=%ld | fs_e2e=%ld ns\n",
         tid, $d, $ret, $fs_e2e);
}

tracepoint:syscalls:sys_exit_close
/ comm=="xfskv_test"/
{
    $d = @dio_cur[pid];
    $cnt = @dio_cnt[pid, $d];
    $avg = ($cnt > 0) ? (@tot_lat_ns[pid, $d] / $cnt) : 0;

    printf("[DIO] tid=%d dio=%d | Num of cmds=%d avg_cmd=%ld ns\n",
         tid, $d, $cnt, $avg);

    delete(@dio_fs_t0[pid, $d]);
    delete(@dio_cnt[pid, $d]);
    delete(@tot_lat_ns[pid, $d]);
    @dio_cur[pid] = 0;
}

END
{
    clear(@start);
    clear(@dio_cnt);
    clear(@tot_lat_ns);
    clear(@dio_seq);
    clear(@dio_fs_t0);
    clear(@dio_cur);
    clear(@cmd_owner_pid);
    clear(@cmd_owner_dio);
}
